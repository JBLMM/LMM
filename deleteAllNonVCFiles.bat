:: Script recursively delete all files and folders that are not used for VC++
:: Everything out of *.h *.cpp *.vcxproj *.vcxproj.filter will be deleted
::
:: %ROOT_LUNA%\.svn folder
:: %ROOT_LUNA%\config
:: contain of the %ROOT_LUNA%\VCBuild  folder
:: recursively delete all *.txt *.cmake files (cmake file)
:: recursively delete all 'CMakeFiles' folder generated by cmake
::
:: see 
:: http://stackoverflow.com/questions/1807425/batch-recursively-delete-all-folders-starting-with
:: http://stackoverflow.com/questions/11269338/how-can-i-comment-out-a-section-in-a-batch-file-and-how-can-i-add-comments-to
:: https://answers.yahoo.com/question/index?qid=20081005123533AALLIPA
:: 
:: script tested only on Windows 7
:: chithanhnguyen.math@gmail.com

:: recursively delete CMakeFiles folders
@echo off
echo * 
echo * - This script will delete files in svn repository
echo * - Be sure that the repository is clean
echo * - If not, commit before running the script
echo * 
SET /P ANSWER=--LUNA REQUEST : is Repository status clean (y/n)?
IF /i {%ANSWER%}=={n} (goto :repositorynotready)
IF /i {%ANSWER%}=={y} (goto :repositoryready)

:repositorynotready
echo LUNA WARNING : commit the working repository before runing this script
GOTO:EOF ::break

:repositoryready
echo *
echo * - LUNA empty config, VCBuild, data\output folders 
DEL /S /Q config\*.* data\output\*.*
RD /S /Q VCBuild\Debug VCBuild\Release

echo *
echo * - LUNA deleting .sdf, .suo, .vcxproj.user, file 
DEL /S /Q *.sdf *.opensdf *.vcxproj.user
DEL /AH *.v11.suo 

echo *
echo * - LUNA deleting ALL_BUILD.* ZERO_CHECK.*, file and Win32, Debug folders
DEL /S /Q ALL_BUILD.* ZERO_CHECK.*
RD /S /Q Win32 Debug

echo *
echo * - LUNA deleting CMakeFiles folders  
FOR /D /R %%X IN (CMakeFiles) DO (
     IF EXIST "%%X" (
 		RD /S /Q "%%X"
 		echo 		- LUNA deleting : %%X
 	)
)

echo *
echo * - LUNA deleting .txt files
FOR /R %%X IN (*.txt) DO (
     IF EXIST "%%X" (
 		DEL /S /Q "%%X"
 	)
)

echo *
echo * - LUNA deleting .cmake files
FOR /R %%X IN (*.cmake) DO (
     IF EXIST "%%X" (
 		DEL /S /Q "%%X"
 	)
)

echo ---
echo * 
echo *	LUNA WARNING
echo *	- Empty the contents of VCBuild folder 
echo *	- Delete .svn 
echo *	- Delete .suo file
echo *  - Delete .bat file
echo *	DO NOT FORGET ReUpdate the repository for revert sources files after zipping VC project